plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

configurations {
    shadowCommon
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    useFabricMixin = true
}

repositories {
    maven {
        name = "JEI Maven"
        url = "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = 'TOP Maven'
        url = "https://maven.tterrag.com"
    }
    maven {
        name = "Curse Maven"
        url = "https://www.cursemaven.com"
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    modApi "me.shedaniel:architectury-forge:${rootProject.architectury_version}"

    modRuntime "mezz.jei:jei-1.16.4:${rootProject.jei_version}"
    modApi "mezz.jei:jei-1.16.4:${rootProject.jei_version}:api"

    modImplementation "mcjty.theoneprobe:TheOneProbe-1.16:${rootProject.top_version}"
    modRuntime "curse.maven:jade-324717:${rootProject.jade_version}"
    modApi "curse.maven:jade-api-324717:${rootProject.jade_api_version}"


    implementation(project(path: ":common")) {
        transitive = false
    }
    developmentForge(project(path: ":common")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    dependsOn shadowJar
    input.set shadowJar.archiveFile
    classifier "forge"
}

jar {
    classifier "dev"
}

java {
    withSourcesJar()
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}